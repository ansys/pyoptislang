!/batch
! --ANSYS-File---
! Definition of forces, length an geometry of the 10bar truss


F1=100000
F2=100000

area01=10.0
area02=10.0
area03=10.0
area04=10.0
area05=10.0
area06=10.0
area07=10.0
area08=10.0
area09=10.0
area10=10.0

fini
/prep7

leng=360
leng2=360*(2)**0.5

mp,ex,1,10000000
mp,dens,1,0.1
mp,nuxy,1,0.3

volume=0
*do,i,1,10,1
    *if,i,lt,10,then
        R,i,area0%i%
    *else
        R,i,area%i%
    *endif

    *if,i,le,6,then
       volume=volume+area0%i%*leng
    *elif i,le,9,then
       volume=volume+area0%i%*leng2
    *else
       volume=volume+area%i%*leng2
    *endif
*enddo

mass=volume*0.1

! The geometry

!5              --1--          3          --2--          1
!
!
!           7        8                   9     10
!            \      /          |          \    /         |
!             \    /           |           \  /          |
!              \  /            5            \/           6
!               \/             |            /\           |
!               /\             |           /  \          |
!              /  \                       /    \
!             /    \
!
!
!6              --3--          4          --4--          2
!                              |                         |
!                             \/                        \/
!                             F1                        F2
!
!
!Coordinates of the nodes

N,1,2*leng,0
N,2,2*leng,-leng
N,3,leng,0
N,4,leng,-leng
N,5,0,0
N,6,0,-leng


ET,1,8

type,1
mat,1

!Connecting the nodes by trusses

real,1  $ E,3,5
real,2  $ E,1,3
real,3  $ E,4,6
real,4  $ E,2,4
real,5  $ E,3,4
real,6  $ E,1,2
real,7  $ E,4,5
real,8  $ E,3,6
real,9  $ E,2,3
real,10 $ E,1,4

!Definition of the constraints

D,5,ALL
D,6,ALL
D,ALL,UZ

F,4,FY,-F1
F,2,FY,-F2


fini
/solu

solve

fini
/post1

!Basis Definition Outputs
total_mass=1.0
max_stress=1.0
max_displacement_4=1.0
max_displacement_2=1.0
Stress1=1.0
Stress2=1.0
Stress3=1.0
Stress4=1.0
Stress5=1.0
Stress6=1.0
Stress7=1.0
Stress8=1.0
Stress9=1.0
Stress10=1.0


set,last
allsel

etable,sx,ls,1
s_max=0

*do,i,1,10,1

*get,stress,elem,i,etab,sx

*if,s_max,ABLT,stress,then
s_max=abs(stress)
*endif

*enddo

!Definition of the parameter values

*get,stress1,elem,1,etab,sx
*get,stress2,elem,2,etab,sx
*get,stress3,elem,3,etab,sx
*get,stress4,elem,4,etab,sx
*get,stress5,elem,5,etab,sx
*get,stress6,elem,6,etab,sx
*get,stress7,elem,7,etab,sx
*get,stress8,elem,8,etab,sx
*get,stress9,elem,9,etab,sx
*get,stress10,elem,10,etab,sx

u_max_2=UY(2)
u_max_4=UY(4)

!Writing of the output-file with the required parameters

/output,ansys_output,txt

total_mass=mass
max_stress=s_max
max_displacement_4=u_max_4
max_displacement_2=u_max_2
Stress1=stress1
Stress2=stress2
Stress3=stress3
Stress4=stress4
Stress5=stress5
Stress6=stress6
Stress7=stress7
Stress8=stress8
Stress9=stress9
Stress10=stress10

/output

fini
!/exit,nosa
